cmake_minimum_required(VERSION 3.7 FATAL_ERROR)
project(Ut99PubSrc LANGUAGES CXX)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD 98)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(SYSTEM_DIR ${PROJECT_SOURCE_DIR}/System)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${SYSTEM_DIR})
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG ${CMAKE_LIBRARY_OUTPUT_DIRECTORY})
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_MINSIZEREL ${CMAKE_LIBRARY_OUTPUT_DIRECTORY})
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE ${CMAKE_LIBRARY_OUTPUT_DIRECTORY})
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELWITHDEBINFO ${CMAKE_LIBRARY_OUTPUT_DIRECTORY})
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${SYSTEM_DIR})
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG ${CMAKE_RUNTIME_OUTPUT_DIRECTORY})
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${CMAKE_RUNTIME_OUTPUT_DIRECTORY})
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE ${CMAKE_RUNTIME_OUTPUT_DIRECTORY})
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${CMAKE_RUNTIME_OUTPUT_DIRECTORY})
if(WIN32)
    if(MSVC)
        add_compile_options("/Zc:wchar_t-")
        set(DXSDK_INCLUDE $ENV{DXSDK_DIR}Include CACHE PATH "The DirectX SDK include path")
        set(DXSDK_LIB $ENV{DXSDK_DIR}Lib/x86 CACHE PATH "The DirectX SDK library path")
    endif(MSVC)
    add_definitions(-D_UNICODE -DUNICODE)
endif(WIN32)
add_definitions(-DUTGLR_NO_APP_MALLOC)
add_subdirectory(Core)
add_subdirectory(Engine)
add_subdirectory(UCC)
add_subdirectory(SampleNativePackage)
add_subdirectory(SamplePackage)
if(WIN32)
    add_subdirectory(Window)
    add_subdirectory(D3DDrv)
    add_subdirectory(OpenGLDrv)
    add_subdirectory(Setup)
elseif(UNIX AND NOT APPLE)
    find_package(X11)
    add_subdirectory(Audio)
    if(X11_FOUND)
        add_subdirectory(XDrv)
        find_package(OpenGL)
        if(OPENGL_XMESA_FOUND)
            add_subdirectory(XMesaGLDrv)
        else(OPENGL_XMESA_FOUND)
            message(WARNING "Could not find XMesa. The XMesaGL renderer will not be built.")
        endif(OPENGL_XMESA_FOUND)
        add_subdirectory(XLaunch)
    else(X11_FOUND)
        message(WARNING "Could not find X11. Targets that require X11 will not be built.")
    endif(X11_FOUND)
else(WIN32)
    message(FATAL_ERROR "Sorry, your target platform isn't supported.")
endif(WIN32)
add_custom_command(OUTPUT ${UNREALSCRIPT_HEADERS} ${UNREALSCRIPT_MODULES}
    COMMAND UCC ARGS make -Silent -NoBind
    DEPENDS ${UNREALSCRIPT_CLASSES}
    WORKING_DIRECTORY ${SYSTEM_DIR})
add_custom_target(UnrealScript ALL DEPENDS ${UNREALSCRIPT_HEADERS} ${UNREALSCRIPT_MODULES})
